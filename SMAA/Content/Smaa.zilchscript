//http://www.iryoku.com/smaa/downloads/SMAA-Enhanced-Subpixel-Morphological-Antialiasing.pdf
//http://www.cs.cmu.edu/afs/cs/academic/class/15869-f11/www/readings/reshetov09_mlaa.pdf
//Practical Morphological Anti-Aliasing GPU-Pro 2


[RunInEditor]
class Smaa : ZilchComponent
{
  
  [Property] var EnableSMAA : Boolean = true;
  [Property] var EnableDiagnols : Boolean = true;
  // Use depth info to reject possibly false edges.
  [Property] var DepthBasedEdges : Boolean = true;
  // Detect more edges with high setting.
  [Property] var EdgeDetection : DetectionChance = DetectionChance.Medium;
  // Switch to Edge and Blend Area to debug intermidiate textures.
  [Property] var TargetTexture : SMAARenderTargets = SMAARenderTargets.FinalBlend;
  //[Property] var ClearColor : Real4 = Real4(0, 0, 0, 1);
  
  function LinearToGamma(event : RenderTasksEvent, inputBuffer : RenderTarget, outputBuffer : RenderTarget)
  {
    var toGamma = LinearToGammaPass();
    toGamma.Texture = inputBuffer.Texture;
    event.AddRenderTaskPostProcess(outputBuffer, toGamma);
  }
  
  function GammaToLinear(event : RenderTasksEvent, inputBuffer : RenderTarget, outputBuffer : RenderTarget)
  {
    var toLinear = GammaToLinearPass();
    toLinear.Texture = inputBuffer.Texture;
    event.AddRenderTaskPostProcess(outputBuffer, toLinear);
  }
  
  function AddRenderTask(event : RenderTasksEvent, lightBuffer : RenderTarget, depth : RenderTarget)
  {
    // Don't bother
    if(!this.EnableSMAA)
      return;
      
    // Sampler settings for filtering edges texture.
    var samplerSettings = SamplerSettings();
    samplerSettings.AddressingX = TextureAddressing.Clamp;
    samplerSettings.AddressingY = TextureAddressing.Clamp;
    samplerSettings.Filtering = TextureFiltering.Bilinear;
    //samplerSettings.CompareFunc = TextureCompareFunc.Always;
    //samplerSettings.CompareMode = TextureCompareMode.Enabled;
    
    var size = lightBuffer.Texture.Size;
    var colorCopy = event.GetRenderTarget(size, lightBuffer.Texture.Format, samplerSettings);
    var edges = event.GetRenderTarget(size, TextureFormat.RG8, samplerSettings);
    var areas = event.GetRenderTarget(size, TextureFormat.RGBA8, samplerSettings);
    var finalBlend = event.GetRenderTarget(size, lightBuffer.Texture.Format, samplerSettings);
    
    var renderSettings = RenderSettings() {CullMode = CullMode.Disabled};
    //renderSettings.DepthTarget = depth;
    //renderSettings.CullMode = CullMode.Disabled;
    //renderSettings.DepthSettings.DepthMode = DepthMode.Disabled;
    //renderSettings.DepthSettings.DepthCompareFunc = TextureCompareFunc.LessEqual;
    
    //Pass uses gamma corrected values and textures. Need input in the same color space.
    //this.LinearToGamma(event, lightBuffer, colorCopy);
    var copyTarget = CopyTarget();
    copyTarget.Texture = lightBuffer.Texture;
    event.AddRenderTaskPostProcess(colorCopy, copyTarget);
    
    // First pass, using predicated thresholding here.
    var postProcess = EdgeDetectionPostProcess();
      postProcess.DepthTexture = depth.Texture;
      postProcess.DepthBasedThresholding = this.DepthBasedEdges;
    if(this.EdgeDetection == DetectionChance.Medium)
      postProcess.ComparisonThreshold = 0.10;
    else
      postProcess.ComparisonThreshold = 0.01;
    postProcess.Texture = colorCopy.Texture;
    renderSettings.ColorTarget = edges;
    //event.AddRenderTaskClearTarget(renderSettings, this.ClearColor, 1.0);
    event.AddRenderTaskClearTarget(edges, Real4(0.0));
    event.AddRenderTaskPostProcess(renderSettings, postProcess);
    
    // Second pass.
    var areasPostProcess = BlendByArea();
    areasPostProcess.DiagEnabled = this.EnableDiagnols;
    if (this.TargetTexture == SMAARenderTargets.Area)
      areasPostProcess.DebugMode = true;
    areasPostProcess.EdgesTexture = edges.Texture;
    areasPostProcess.SearchTexture = Texture.SearchTex;
    areasPostProcess.AreaTexture = Texture.AreaTex;
    areasPostProcess.SearchSamples = 8.0;
    renderSettings.ColorTarget = areas;
    //event.AddRenderTaskClearTarget(renderSettings, this.ClearColor, 1.0);
    event.AddRenderTaskClearTarget(renderSettings, Real4(0.0));
    event.AddRenderTaskPostProcess(renderSettings, areasPostProcess);
    
    // Final pass.
    var blendPostProcess = NeighborhoodBlend();
    blendPostProcess.AreaTexture = areas.Texture;
    blendPostProcess.ColorTexture = colorCopy.Texture;
    renderSettings.ColorTarget = finalBlend;
    //event.AddRenderTaskClearTarget(renderSettings, this.ClearColor, 1.0);
    event.AddRenderTaskPostProcess(renderSettings, blendPostProcess);

    // Finally, copy to render output.
    var renderPass = CopyTarget();

    // Debug options
    if (this.TargetTexture == SMAARenderTargets.FinalBlend)
      renderPass.Texture = finalBlend.Texture;
    if (this.TargetTexture == SMAARenderTargets.Area)
      renderPass.Texture = areas.Texture;
    if (this.TargetTexture == SMAARenderTargets.Edge)
      renderPass.Texture = edges.Texture;
      
    //renderSettings.ColorTarget = colorCopy;
    renderSettings.ColorTarget = lightBuffer;
    //event.AddRenderTaskClearTarget(renderSettings, this.ClearColor, 1.0);
    event.AddRenderTaskPostProcess(renderSettings, renderPass);
    
    // The renderer must be doing a final LinearToGamma pass in Deferred Renderer.
    // Set the values back to linear space for other operations that might come after smaa.
    //this.GammaToLinear(event, colorCopy, lightBuffer);

    // Release the rendertargets used for SMAA.
    colorCopy.Release();
    edges.Release();
    areas.Release();
    finalBlend.Release();
  }
}

// Settings Enums.
enum SMAARenderTargets
{
  Edge,
  Area,
  FinalBlend
}

enum DetectionChance
{
  High,
  Medium
}